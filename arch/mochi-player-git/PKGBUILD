# Maintainer: u8sand <u8sand@gmail.com>
# Contributor: Alfredo Ramos <alfredo dot ramos at yandex dot com>

_pkgname=mochi-player
pkgname=${_pkgname}-git
pkgver=2.1.0
pkgrel=1
pkgdesc='A free and open source, cross-platform, libmpv based multimedia player. Qt5 build. Development version.'
arch=('i686' 'x86_64')
url='http://mochi-player.github.io/'
license=('GPL')

depends=('mpv' 'qt5-declarative' 'qt5-svg')
optdepends=(
	'qt5-x11extras: for X11 support'
	'qt5-wayland: for wayland support'
	'qt5-translations: for localization'
	'youtube-dl: for remote video streaming'
	'noto-fonts: for proper font support'
	'mpv-git: for bleeding-edge mpv features'
)
makedepends=('git' 'qt5-tools')
provides=("${_pkgname}=${pkgver}")
conflicts=("${pkgname}-git")
replaces=("${pkgname}-git")

source=(
	"${_pkgname}::git+https://github.com/mochi-player/mochi-player.git"
)
sha512sums=(
	"SKIP"
)

pkgver() {
	cd ${srcdir}/${_pkgname}
	(
		set -o pipefail
		git describe --long --tags | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
	mkdir ${srcdir}/build
}

build() {
	cd ${srcdir}/build
	qmake-qt5 ../${_pkgname}/src -release -spec linux-g++
	make -j $(grep -c ^processor /proc/cpuinfo)
}

package() {
	cd ${srcdir}/build
	make INSTALL_ROOT=${pkgdir} install
}
